
# .tcshrc

# thanks to all the little people:
# http://howto.unixdev.net/tcshrc.html
# http://www.dotfiles.com
# ...
# and probably a lot more


# I NEED TO DO THIS:
    # You can detect an interactive shell from a non interactive one 
    # by testing for the $prompt variable, like this:

      # if ($?prompt) then
        # bindkey ^W backward-delete-word
        # set prompt = "$USER@$HOSTNAME> "
        # ...
      # endif


# precmd = before prompt
# postcmd = before exec


## PATH... should come before everything else
# Future:
#  I should include a util that: 
#   - searches /etc /usr/etc /usr/local/etc /usr/nekoware/etc for execable files
#   - links them to /usr/etcbin or something like that
#   - man that is an abomination
set p=() # Make the $PATH not suck:
# don't include: /usr/ucb /usr/sfw/bin
foreach x ( /{,s}bin /usr/{,s}bin /usr/local/{,s}bin \
            /usr/games{,/bin} /usr/X11R6/{,s}bin \
            /usr/opt/surfraw/bin /opt/ast/bin ~/opt/{,s}bin ~/opt/local/bin \
            /sw/{,s}bin /usr/texbin \
            /usr/pkg/{,s}bin \
            /usr/nekoware/{,s}bin \
            /opt/gcc.3.3/bin /opt/gcc.3.3/bin/i586-pc-interix3 \
            /usr/contrib/bin /usr/contrib/win32/bin /usr/examples/admin \
            /opt/csw/{,s}bin /opt/csw/flex/{,s}bin /opt/csw/gcc4/{,s}bin \
            /opt/SUNWspro/{,s}bin \
            )
  if (-d $x) set p=( $p $x )
end
set path = ( $p )
unset p x


# ls... the search for GNU
set ls = "/bin/ls"
if ( -X gls ) set ls = "gls"
# GNU sed
if ( -X gsed ) alias sed "gsed"
# GNU du for the -h option :):):)
if ( -X gdu ) alias du gdu
# GNU dircolors
if (-X gdircolors) gdircolors -c ~/doc/remote/dhd/hbase/.dir_colors
else if (-X dircolors) dircolors -c ~/doc/remote/dhd/hbase/.dir_colors
endif


######################
# Checking For Stuff #
######################

set uname = `uname`
set host = `hostname`

# GNUstep
if ( -fx /usr/local/GNUstep/System/Library/Makefiles/GNUstep.csh ) then
	source /usr/local/GNUstep/System/Library/Makefiles/GNUstep.csh
else if ( -fx /usr/GNUstep/System/Library/Makefiles/GNUstep.csh ) then
	source /usr/GNUstep/System/Library/Makefiles/GNUstep.csh
endif

# set my browsah
set surf = ''
if ( -d /cygdrive ) then    # Cygwin
    # it inherits the Windows path, so if your Windows path has this set, 
    #  then it will be wrong:
    setenv SVN_SSH /usr/bin/ssh  
    # lists.gnu.org/archive/html/help-emacs-windows/2002-10/msg00109.html:
    setenv CYGWIN "binmode ntsec stty"	# I don't know what does what here
    set surf = "/cygdrive/c/Program\ Files/Mozilla\ Firefox/firefox.exe"
else if ( -d /dev/fs ) then # SFU/SUA
    setenv SVN_SSH /usr/pkg/bin/ssh
    set surf = "/dev/fs/C/Program\ Files/Mozilla\ Firefox/firefox.exe"
    if ( -f /usr/examples/win32/aliases.csh ) source /usr/examples/win32/aliases.csh
else if ( $uname == Darwin ) then # Mac OS X
    set surf = 'open -f "$argv" /Apps/Net\ -\ Web/Camino.app'
    test -r /sw/bin/init.csh && source /sw/bin/init.csh  # fink
else if ( $uname == SunOS ) then  # Solaris
    setenv CC "/opt/csw/gcc4/bin/gcc"
    alias psa ps -ef
    alias psaf 'ps -ef|grep -i \!*'
endif

alias surf $surf
alias firefox $surf
alias stfu $surf


##################
# Global Aliases #
##################

alias ppath  'echo $PATH | sed "s/:/\n/g"'
alias pupath 'echo $PATH | sed "s/:/\n/g" | sort | uniq'
alias logrec 'lsl /var/log | grep -v \\.bz2 | grep -v \\.0 | grep "`date +%b\ %d\ %k`"'

alias psa ps ax
alias psaf 'ps ax|grep -i \!*'

alias .. 'cd ..'
alias c clear
alias df df -h
alias h history
alias m more
alias l less

alias ls '$ls -hF --color'
alias lsa '$ls -ahF --color'
alias lsl '$ls -alhF --color'
alias l1 '$ls -1hF --color'
alias llm '$ls -lahrtF --color' # lists by last modification time (recently modified files at the end)

alias pu pushd
alias po popd
alias ff "find . -name '\!*' -print"
alias trash 'mv $argv ~/.Trash/'
alias tailmes 'tail -f /var/log/messages'
alias mess less /var/log/messages
alias dmesg 'dmesg|less'
if ( -X wn ) alias define 'wn \!* -over'

#gentoo junk: 
alias ugrep "grep -i $1 /usr/portage/profiles/use{,.local}.desc"
alias fnc 'find /etc -iname "._cfg????*"'
# of use only on linux.. 
# might want to rewrite this for when I have boot mounted, but ro. 
alias kinst "mount /boot && cp arch/i386/boot/bzImage /boot/bzImage && umount /boot"


# under OS X, `posd` tells you where the topmost Finder window is. (in fink)
# `fdc` changes the directory of the shell to match that of the
# topmost Finder window. This alias is the reverse behavior. 
alias cdf 'cd "`posd`"' # requires posd (in fink). 

alias arin      'whois -h whois.arin.net'
alias ripe      'whois -h whois.ripe.net'
alias apnic     'whois -h whois.apnic.net'

alias co_homebase "mkdir -p ~/doc/remote/dhd && svn co svn+ssh://mrled@mrled.org/home/mrled/svn/dhd doc/remote/"

alias omg		'echo wtf'


###################
# Global Settings #
###################

# tcsh settings
set correct=none # don't correct me... I know I'm right!
set autolist # not only can tcsh list completions, it can do it in color!
set color    # ...
set colorcat # ...
set noding # disable annoying DING!
unset ampm # set 24-hr time
set listjobs=long # list jobs long
set listlinks # ls-F builtin will list the filetype to which a symlink points
set matchbeep=never # turn off beeping for ambiguous completion (so annoying)
set savehist # save history between sessions
set watch=(any any) # print logins and logouts
set autologout=0 # autologout drives me crazy
set notify # tell me emediately when jobs complete; don't wait for prompt
set noclobber # > only into new files; >> only into existing files
set history = 100
set savehist = 100

# YES! correct backslash escape sequences: 
# backslashes always quote the following characters: \'"
# not default b/c it creates errors with some csh scripts. 
set backslash_quote

# will set my pager to be the best available
where more >/dev/null && setenv PAGER more
where less >/dev/null && setenv PAGER less
#where most >/dev/null && setenv PAGER most

# set my editor to be correct
# (the -nw tells it not to open up a new window)
setenv EDITOR "emacs"
setenv VISUAL "emacs"
setenv FSEDIT "emacs"

# set my edit mode to emacs
# why in the WORLD would someone set this to vi by default?
# no new freaking user wants to learn how to enter "edit mode"
# to edit his command history. STUPID!
# (Interix, at least, does this wrong)
bindkey -e
bindkey ^G kill-whole-line #behaves somewhat more like emacs

umask 077 #stop reading my files!!
if (-X mesg) mesg y # turn on talk
#alias postcmd "rehash" # see new apps after install. SLOW

# Setting the default prompt
# Gentoo default:  
# set prompt='[%n@%m %c]# '
# Mine: looks like the following, but in color
# 12:14:46 micah@AndrAIa micah % 
set prompt='%{\033[1m%}%P %{\033[32m%}%n@%m %{\033[34m%}%C%#%{\033[0m%} '

# the rprompt is a string printed on the right hand side of the screen
# (after the command input) when the prompt is being displayed on the
# left. recognizes same formatting as 'prompt'. automatically appears 
# and reappears as necessary. appears only when it fits. 
#set rprompt='somethinghere?'



###############
# Completions #
###############

complete find \
	'n/-name/f/' \
	'n/-type/(c b d f p l s)/' \
	'n/-exec/c/' \
	'c/-/(name type user size ctime mtime atime print exec )/' \
	'n/*/d/'
