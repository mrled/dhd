# .tcshrc

# I NEED TO DO THIS:
    # You can detect an interactive shell from a non interactive one 
    # by testing for the $prompt variable, like this:

      # if ($?prompt) then
        # bindkey ^W backward-delete-word
        # set prompt = "$USER@$HOSTNAME> "
        # ...
      # endif

    # this sounds cool: 
    # setenv PATH = "${HOME}/bin/${HOSTNAME}:${HOME}/bin/`uname -s`:"


# thanks to all the little people:
# http://howto.unixdev.net/tcshrc.html
# http://www.dotfiles.com
# ...
# and probably a lot more

# precmd = before prompt
# postcmd = before exec


#
# TODO
#
# organize data rightly right
# setup a single command that searches whatever package manager I use for what I'm 
# looking for. needs to be able to search more than one (eg fink, dports; solaris, 
# pkgsrc; etc)
#      if portage, then (if esearch, then esearch; else emerge -s)
#      if fink, then
#      if darwinports, then
#      if solaris, then
#      if pkgsrc, then
#
# $UNAME section
# $HOST section
# Package Manager Section


# A cool idea:
# have 2 files:
#	.tcshrc - unique per machine
#	.tcsh/mrl - or something. stuff that is applicable everywhere
# have settings in .tcshrc and have them read and reacted to in .tcsh/mrl
# eg: 
#	.tcshrc: setenv MY_LS "/usr/pkg/bin/gls" && setenv MY_LS_TYPE "gnu"
#	.tcsh/mrl: if ( MY_LS_TYPE == "gnu") then 
#			alias ls "$MY_LS -hF --color"
# etc. 
# 
# A better idea:
# have the same two files, except .tcshrc is global, and it calls .tcsh/local
# or .tcsh/$hostname or something, which is unique per machine.
# Could also call .tcsh/$uname to have unique per OS options (eg all NetBSD 
# machines check for gls and use it instead of ls)

########
# new, as yet uncategorized: 
########

setenv EDU "~/doc/edu/2007-1(spring)"
setenv EDU1 "~/doc/edu/2007-1(spring)/.1"
setenv EDU2 "~/doc/edu/2007-1(spring)/.2"
setenv EDU3 "~/doc/edu/2007-1(spring)/.3"
setenv EDU4 "~/doc/edu/2007-1(spring)/.4"
setenv EDU5 "~/doc/edu/2007-1(spring)/.5"

setenv NLTK_LITE_CORPORA "${HOME}/opt/words/nltk_lite_corpora"

alias logrec 'ls -alF --color /var/log | grep -v \\.bz2 | grep -v \\.0 | grep "`date +%b\ %d\ %k`"'

# of use only on linux.. 
# might want to rewrite this for when I have boot mounted, but ro. 
alias kinst "mount /boot && cp arch/i386/boot/bzImage /boot/bzImage && umount /boot"

# for cygwin:
# this makes emacs' command: C-x C-c work properly (quit emacs)
# instead of be interpretted as C-x C-g
# more info here:

# a WAAAAY cool idea about prompt colors is here
# http://www.dotfiles.com/files/21/442_dot.tcshrc
# (also mirrored locally in my tcsh examples dir

######################
# Checking For Stuff #
######################

setenv UNAME `uname`
setenv UNAMEA `uname -a`
setenv HOST `hostname`

# Checking for stuff-specific stuff


#default cases:
alias psa ps ax
alias psaf 'ps ax|grep -i \!*'

 
# fink
test -r /sw/bin/init.csh && source /sw/bin/init.csh

# GNUstep
if ( -fx /usr/local/GNUstep/System/Library/Makefiles/GNUstep.csh ) then
	source /usr/local/GNUstep/System/Library/Makefiles/GNUstep.csh
else if ( -fx /usr/GNUstep/System/Library/Makefiles/GNUstep.csh ) then
	source /usr/GNUstep/System/Library/Makefiles/GNUstep.csh
endif

# Cygwin
if ( -d /cygdrive ) then 
    # it inherits the Windows path, so if your Windows path has this set, 
    #  then it will be wrong:
    setenv SVN_SSH /usr/bin/ssh  
    # lists.gnu.org/archive/html/help-emacs-windows/2002-10/msg00109.html:
    setenv CYGWIN "binmode ntsec stty"	# I don't know what does what here
endif

if ( $UNAME == SunOS ) then
    setenv CC "/opt/csw/gcc4/bin/gcc"
    alias psa ps -ef
    alias psaf 'ps -ef|grep -i \!*'

endif


##################
# Global Aliases #
##################

alias .. 'cd ..'
alias c clear
alias df df -h
alias h history
alias m more
alias l less

alias ls '$ls -hF --color'
alias lsa '$ls -ahF --color'
alias lsl '$ls -alhF --color'
alias l1 '$ls -1hF --color'
alias llm '$ls -lahrtF --color' # lists by last modification time (recently modified files at the end)

alias pu pushd
alias po popd

alias ff "find . -name '\!*' -print"

alias trash 'mv $argv ~/.Trash/'

alias tailmes 'tail -f /var/log/messages'
alias mess less /var/log/messages
alias dmesg 'dmesg|less'

if ( -X gsed ) set sed=gsed
else set sed=sed
endif

alias ppath  'echo $PATH | $sed "s/:/\n/g"'
alias pupath 'echo $PATH | $sed "s/:/\n/g" | sort | uniq'

#gentoo junk: 
alias ugrep "grep -i $1 /usr/portage/profiles/use{,.local}.desc"
alias fnc 'find /etc -iname "._cfg????*"'

if ( -X wn ) alias define 'wn \!* -over'


# under OS X, posd tells you where the topmost Finder window is. 
# this is installable via fink (apt-get install posd)
# that package also installs the utility "fdc"
# which changes the directory of the shell to match that of the
# topmost Finder window. This alias is the reverse behavior. 
alias cdf 'cd "`posd`"'

#
# This is the "new command" section for stuff that might 
# should just go in various shell scripts
#

alias arin      'whois -h whois.arin.net'
alias ripe      'whois -h whois.ripe.net'
alias apnic     'whois -h whois.apnic.net'

alias omg		'echo wtf'


###################
# Global Settings #
###################

# tcsh settings
set correct=none # don't correct me... I know I'm right!
set autolist # not only can tcsh list completions, it can do it in color!
set color    # ...
set colorcat # ...
set noding # disable annoying DING!
unset ampm # set 24-hr time
set listjobs=long # list jobs long
set listlinks # ls-F builtin will list the filetype to which a symlink points
set matchbeep=never # turn off beeping for ambiguous completion (so annoying)
set savehist # save history between sessions
set watch=(any any) # print logins and logouts
set autologout=0 # autologout drives me crazy
set notify # tell me emediately when jobs complete; don't wait for prompt
set noclobber # > only into new files; >> only into existing files


# YES! correct backslash escape sequences: 
# backslashes always quote the following characters: \'"
# not default b/c it creates errors with some csh scripts. 
set backslash_quote

# will set my pager to be the best available
where more >/dev/null && setenv PAGER more
where less >/dev/null && setenv PAGER less
where most >/dev/null && setenv PAGER most

# set my editor to be correct
# (the -nw tells it not to open up a new window)
setenv EDITOR "emacs"
setenv VISUAL "emacs"
setenv FSEDIT "emacs"

# set my edit mode to emacs
# why in the WORLD would someone set this to vi by default?
# no new freaking user wants to learn how to enter "edit mode"
# to edit his command history. STUPID!
# (Interix, at least, does this wrong)
bindkey -e
bindkey ^G kill-whole-line #behaves somewhat more like emacs

umask 077 #stop reading my files!!
if (-X mesg) mesg y # turn on talk
#alias postcmd "rehash" # see new apps after install. SLOW

# Setting the default prompt
# Gentoo default:  
# set prompt='[%n@%m %c]# '
# Mine: looks like the following, but in color
# 12:14:46 micah@AndrAIa micah % 
set prompt='%{\033[1m%}%P %{\033[32m%}%n@%m %{\033[34m%}%C%#%{\033[0m%} '

# the rprompt is a string printed on the right hand side of the screen
# (after the command input) when the prompt is being displayed on the
# left. recognizes same formatting as 'prompt'. automatically appears 
# and reappears as necessary. appears only when it fits. 
#set rprompt='somethinghere?'



set p=() # Make the $PATH not suck:
# don't include: /usr/ucb /usr/sfw/bin
foreach x ( /{,s}bin /usr/{,s}bin /usr/local/{,s}bin \
            /usr/games{,/bin} /usr/X11R6/{,s}bin \
            /usr/opt/surfraw/bin ~/opt/{,s}bin ~/opt/local/bin \
            /sw/{,s}bin /usr/texbin \
            /usr/pkg/{,s}bin \
            /opt/csw/{,s}bin /opt/csw/flex/{,s}bin /opt/csw/gcc4/{,s}bin \
            /opt/SUNWspro/{,s}bin \
            )
  if (-d $x) set p=( $p $x )
end
set path = ( $p )
unset p x

# ls... the search for GNU
if ( -X gls ) set ls = "gls" # lots of OS prepend g to gnu tools; eg 'gls'
else set ls = ls # "ls-F" is built in to tcsh; tries to behave like GNU
endif

# autogen the MANPATH from PATH
# while I'm at it, I might as well clean up the path...
#  no dup entries
#  no multiple ':'
# setenv CLEANPATH = \
#   echo $PATH | sed s/:/\\n/g | sed /^\$/d | sort | uniq | sed s/\$/:/g | tr -d "\n"
# setenv CLEANMANPATH = \
#   (...)

# GNU dircolors
# has to be done after PATH, in case, eg, it's in /sw/bin or something
if (-X dircolors) test -r ~/.dir_colors && eval `dircolors -c ~/.dir_colors`
else if (-X gdircolors) test -r ~/.dir_colors && eval `gdircolors -c ~/.dir_colors`
endif


###############
# Completions #
###############

complete find \
	'n/-name/f/' \
	'n/-type/(c b d f p l s)/' \
	'n/-exec/c/' \
	'c/-/(name type user size ctime mtime atime print exec )/' \
	'n/*/d/'

##############
# Subversion #
##############
alias co_homebase "svn co svn+ssh://kerrigan/home/export/svnrepo/mrl/home/base ."
