# .tcshrc

# I NEED TO DO THIS:
    # You can detect an interactive shell from a non interactive one 
    # by testing for the $prompt variable, like this:

      # if ($?prompt) then
        # bindkey ^W backward-delete-word
        # set prompt = "$USER@$HOSTNAME> "
        # ...
      # endif

# thanks to all the little people:
# http://howto.unixdev.net/tcshrc.html
# http://www.dotfiles.com
# ...
# and probably a lot more

# precmd = before prompt
# postcmd = before exec


#
# TODO
#
# organize data rightly right
# setup a single command that searches whatever package manager I use for what I'm looking for. needs to be able to search more than one (eg fink, dports; solaris, pkgsrc; etc)
#      if portage, then (if esearch, then esearch; else emerge -s)
#      if fink, then
#      if darwinports, then
#      if solaris, then
#      if pkgsrc, then
#
# $UNAME section
# $HOST section
# Package Manager Section


# A cool idea:
# have 2 files:
#	.tcshrc - unique per machine
#	.tcsh/mrl - or something. stuff that is applicable everywhere
# have settings in .tcshrc and have them read and reacted to in .tcsh/mrl
# eg: 
#	.tcshrc: setenv MY_LS "/usr/pkg/bin/gls" && setenv MY_LS_TYPE "gnu"
#	.tcsh/mrl: if ( MY_LS_TYPE == "gnu") then 
#			alias ls "$MY_LS -hF --color"
# etc. 
# 
# A better idea:
# have the same two files, except .tcshrc is global, and it calls .tcsh/local
# or .tcsh/$hostname or something, which is unique per machine.
# Could also call .tcsh/$uname to have unique per OS options (eg all NetBSD 
# machines check for gls and use it instead of ls)

########
# new, as yet uncategorized: 
########

alias logrec 'ls -alF --color /var/log | grep -v \\.bz2 | grep -v \\.0 | grep "`date +%b\ %d\ %k`"'

# of use only on linux.. 
# might want to rewrite this for when I have boot mounted, but ro. 
alias kinst "mount /boot && cp arch/i386/boot/bzImage /boot/bzImage && umount /boot"

# for cygwin:
# this makes emacs' command: C-x C-c work properly (quit emacs)
# instead of be interpretted as C-x C-g
# more info here:
# http://lists.gnu.org/archive/html/help-emacs-windows/2002-10/msg00109.html
setenv CYGWIN "binmode ntsec stty"	# I don't know what does what here

# a WAAAAY cool idea about prompt colors is here
# http://www.dotfiles.com/files/21/442_dot.tcshrc
# (also mirrored locally in my tcsh examples dir

######################
# Checking For Stuff #
######################

setenv UNAME `uname`
setenv UNAMEA `uname -a`
setenv HOST `hostname -s`

# Checking for stuff-specific stuff

# ls... the search for GNU
# perhaps I can substitute the builtin ls-F function for this?
# or this is a great place for my 2-rc-files idea. 
# fink's GNU ls
if ( -fx /sw/bin/ls ) then
	set ls = "/sw/bin/ls"
# darwinports' GNU ls
else if ( -fx /opt/local/bin/ls ) then
	set ls = "/opt/local/bin/ls"
# netbsd prepends g to the gnu tools; eg 'gls'
else if ( -X gls ) then
	set ls = "gls"
# ls-F function built into tcsh. tries to behave like GNU
else 
	set ls = "ls-F"
endif

####### sdf.lonestar.org - various weirdnesses
### notify
# had to execyte this next line in a subshell - that is, use () - 
# because it wouldn't recognize it correctly otherwise. 
# THIS DOESN'T WORK
#alias notify-builtin '(unalias notify && notify)'
#alias notify-sdf '/usr/pkg/bin/notify'
#alias notify 'printf "Do you want /usr/pkg/bin/notify? \n\tThen type notify-sdf\nDo you want tcsh builtin notify?\n\tThen type notify-builtin\n"' 
###

# GNUstep
if ( -fx /usr/local/GNUstep/System/Library/Makefiles/GNUstep.csh ) then
	source /usr/local/GNUstep/System/Library/Makefiles/GNUstep.csh
else if ( -fx /usr/GNUstep/System/Library/Makefiles/GNUstep.csh ) then
	source /usr/GNUstep/System/Library/Makefiles/GNUstep.csh
endif

# Cygwin
if ( -d /cygdrive ) then 
# it inherits the Windows path, so if your Windows path has this set, it will be wrong
    setenv SVN_SSH /usr/bin/ssh  
endif


######################
# Package Management #
######################

# y'know... I've been thinking...
# maybe I can just put this in a shell script. 
# that way it won't have to load every time...

# my goals for this section:
# - to set the path properly (for those that require it, something special: eg, fink)
# - to have a utility for searching no matter what my package manager is
#	eg, if portage, then alias package to emerge -S (or if esearch, emerge package to esearch).
# I could have the package utility in a separate script, but I couldn't set the path there
# however, that would go well with my dynamic path setting (either below, or in an example somewhere, I forget where. 
# so I think I'm going to try that. hrm. 

# so I don't inherit anything silly
unsetenv PMAN_*

# Gentoo's portage
if ( -d /usr/portage ) then
	setenv PMAN_PORTAGE "true"
	alias ugrep 'grep \!* /usr/portage/profiles/use.desc /usr/portage/profiles/use.local.desc'
	alias fnc 'find /etc -iname "._cfg????*"'
endif

# Fink (apt-get on OS X)
if ( -f /sw/bin/init.csh ) then
	set pman_fink=true
	test -r /sw/bin/init.csh && source /sw/bin/init.csh
endif 

# DarwinPorts (FreeBSD's ports on OS X)
if ( -d /opt/local ) then
	set pman_dports=true
	# so that I can do "ls $DPLP/aqua/AquaLess":
	setenv DPLP "/opt/local/var/db/dports/sources/rsync.rsync.opendarwin.org_dpupdate_dports/"
endif

# Interix + pkg_add from InteropSystems (eg not pkgsrc from pkgsrc.org)
if ( -f /usr/local/bin/pkg_add ) then
	set pman_interix=true
	# obviously, this next line requires SFU 3.5, but it also requires
	# a .netrc with a username and password line
	# see interopsystems.com for more details
	setenv PKG_PATH "ftp://ftp.interopsystems.com/pkgs/3.5/"
endif

# NETBSD'S pkgsrc from pkgsrc.org (multi-platform?)
if ( -d /usr/pkg ) then
	set pman_pkgsrc=true
endif


# Solaris's package system
#	alias pa 'pkgadd -d .'

##################
# Global Aliases #
##################

alias .. 'cd ..'
alias c clear
alias df df -h
alias h history
alias m more
alias l less

alias ls '$ls -hF --color'
alias lsa '$ls -ahF --color'
alias lsl '$ls -alhF --color'
alias l1 '$ls -1hF --color'

alias psa ps ax
alias psaf 'ps ax|grep -i \!*'

alias pu pushd
alias po popd

alias ff "find . -name '\!*' -print"


alias tailmes 'tail -f /var/log/messages'
alias mess less /var/log/messages
alias dmesg 'dmesg|less'

# under OS X, posd tells you where the topmost Finder window is. 
# this is installable via fink (apt-get install posd)
# that package also installs the utility "fdc"
# which changes the directory of the shell to match that of the
# topmost Finder window. This alias is the reverse behavior. 
alias cdf 'cd "`posd`"'

#
# This is the "new command" section for stuff that might 
# should just go in various shell scripts
#

alias arin      'whois -h whois.arin.net'
alias ripe      'whois -h whois.ripe.net'
alias apnic     'whois -h whois.apnic.net'

alias omg		'echo wtf'


###################
# Global Settings #
###################

# tcsh settings
# don't correct me... I know I'm right!
set correct=none
# not only can tcsh list completions, it can do it in color!
set autolist 
set color 
set colorcat
# disable annoying DING!
set noding
# set 24-hr time
unset ampm
# list jobs long
set listjobs=long
# ls-F builtin will list the type of file to which a symlink points
set listlinks
# turn off beeping for ambiguous completion (this is so annoying)
set matchbeep=never
# save history between sessions (default?)
#set savehist
# print logins and logouts
#set watch=(any any)


# YES! correct backslash escape sequences: 
# backslashes always quote the following characters: \'"
# why not the default? 
# b/c it creates errors with some csh scripts. 
set backslash_quote

# will set my pager to be the best available
where more >/dev/null && setenv PAGER more
where less >/dev/null && setenv PAGER less
where most >/dev/null && setenv PAGER most

# set my editor to be correct
# (the -nw tells it not to open up a new window)
setenv EDITOR "emacs -nw"
setenv VISUAL "emacs -nw"
setenv FSEDIT "emacs -nw"

# set my edit mode to emacs
# why in the WORLD would someone set this to vi by default?
# no new freaking user wants to learn how to enter "edit mode"
# to edit his command history. STUPID!
# (Interix, at least, does this wrong)
bindkey -e

# umask defaults such that no one else can read my files. 
umask 077

# turn on talk
mesg y

# cause new programs to be recognized after installed
#alias postcmd "rehash"

# Setting the default prompt
# Gentoo default:  
# set prompt='[%n@%m %c]# '
# Mine: looks like the following, but in color
# 12:14:46 micah@AndrAIa micah % 
set prompt='%{\033[1m%}%P %{\033[32m%}%n@%m %{\033[34m%}%C%#%{\033[0m%} '

# the rprompt is a string printed on the right hand side of the screen
# (after the command input) when the prompt is being displayed on the
# left. recognizes same formatting as 'prompt'. automatically appears 
# and reappears as necessary. appears only when it fits. 
#set rprompt='somethinghere?'



# PATH stuff
#set path = (/usr/man /usr/*/man /opt/*/man)
#setenv MANPATH "$PATH"
#set path = (/opt/*/{,s}bin /usr/*/{,s}bin /{,s}bin /usr/{,s}bin /usr/games /usr/ucb ~/opt/{,s}bin ~micah/{,s}bin ~vlack/{,s}bin)
#set path = ($path ~/opt/arch/`uname -s`/bin/`uname -m` ~/opt/bin ~/opt/arch/neutral/bin)

# here is where a unique-per-system rc file and a global, unchanging rc file would be really handy:
setenv LOCALPATH "/usr/games/bin:/usr/games:/opt/bzflag-bin/bin:/opt/local/bin:/opt/local/sbin:${HOME}/opt/bin:/Users/micah/opt/bin:/bin:/sbin:/usr/bin:/usr/sbin:/usr/local/bin:/usr/local/sbin:"
setenv PATH "${PATH}:${LOCALPATH}:"

# GNU dircolors
# has to be done after PATH, in case, eg, it's in /sw/bin or something
test -r ~/.dir_colors && eval `dircolors -c ~/.dir_colors`


###############
# Completions #
###############

#complete find \
#	'n/-name/f/' \
#	'n/-type/(c b d f p l s)/' \
#	'n/-exec/c/' \
#	'c/-/(name type user size ctime mtime atime print exec )/' \
#	'n/*/d/'

##############
# Subversion #
##############
alias co_homebase "svn co svn+ssh://kerrigan/home/export/svnrepo/mrl/home/base ."
